#include "stdlib.fc";
#include "tvm.lib.fc";
#include "errors.lib.fc";
#include "hashmap.lib.fc";

(cell, int) merkle_proof::load_cell(cell c) impure inline {
  (slice proof_slice, int exotic?) = c.begin_parse_special();
  throw_unless(err::invalid_merkle_proof_cell, exotic?);
  throw_unless(err::invalid_merkle_proof_cell, proof_slice~load_uint(8) == 3);
  cell pruned_cell = proof_slice~load_ref();
  return (pruned_cell, pruned_cell.cell_hash_0());
}

cell merkle_proof::find_tx(
  cell merkle_proof,
  slice account_addr,
  slice tx_lt
) impure inline {
  (cell ref_cell, int ref_hash) = merkle_proof.merkle_proof::load_cell();
  slice block = ref_cell.begin_parse();
  int tag = block~load_uint(32);
  throw_unless(err::invalid_block_tag, tag == 0x11ef55aa);
  ;; load ref with extra
  slice extra = block.load_ref_3().begin_parse();
  ;; load account_blocks
  slice s = extra.load_ref_2().begin_parse();
  cell account_blocks = s~load_dict();
  ;; assume there is only one account - all other are pruned
  slice acc_block = account_blocks.hm_get(256, account_addr);
  ;; skip CurrencyCollection (extra)
  acc_block~load_coins();
  acc_block~load_dict();
  throw_unless(err::invalid_account_block, acc_block~load_uint(4) == 5);
  acc_block~load_prefix(account_addr);
  ;; remaining part of acc_block is a hashmapAug with transactions.
  slice tx_aug = acc_block.hmslice_get(64, tx_lt);
  ;; skip CurrencyCollection (extra)
  tx_aug~load_coins();
  tx_aug~load_dict();
  cell tx = tx_aug~load_ref();
  return tx;
}
