#include "../libs/tvm.lib.fc";

(int) check_is_not_masterchain_block(slice block_info) inline {
    int block_tag = block_info~load_uint(32);
    throw_unless(err::wrong_block_info, block_tag == 0x9bc7a987);
    block_info~skip_bits(32);
    int not_master = block_info~load_uint(1);
    return not_master;
}

(slice) build_signing_message(int root_hash, int file_hash) inline {
  return begin_cell()
  .store_uint(0x706e0bc5, 32)
  .store_uint(root_hash, 256)
  .store_uint(file_hash, 256)
  .end_cell()
  .begin_parse();
}

(int) check_block_signatures(slice signing_message, cell signatures) inline {
  int signed_weight = 0;
  
  ( int pubkey,
    slice signature,
    int ok?
  ) = signatures.udict_get_next?(256, 0);

  while (ok?) {
    (slice validator_s, int validator_found?) = g::validators.udict_get?(256, pubkey);
    throw_unless(err::wrong_validators_epoch, validator_found?);
    if (check_data_signature(signing_message, signature, pubkey)) {
      int validator_weight = validator_s~load_uint(64);
      signed_weight = signed_weight + validator_weight;
      if (signed_weight * 3 >= 2 * g::total_weight) {
        return -1;
      }
    }

    ( pubkey,
      signature,
      ok?
    ) = signatures.udict_get_next?(256, pubkey);
  }

  return signed_weight * 3 >= 2 * g::total_weight;
}

(slice) load_block_custom(slice cs) inline {
    cs~load_ref();
    cs~load_ref();
    cs~load_ref();
    slice extra_s = cs~load_ref_slice();
    int tag = extra_s~load_uint(32);
    throw_unless(err::wrong_block_extra, tag == 0x4a33f6fd);
    extra_s~load_ref();
    extra_s~load_ref();
    extra_s~load_ref();
    slice custom_s = extra_s~load_ref_slice();
    int custom_magic = custom_s~load_uint(16);
    throw_unless(err::unknown_operation, custom_magic == 0xcca5);
    int is_keyblock = custom_s~load_uint(1);
    throw_unless(err::is_not_keyblock, is_keyblock);
    return custom_s;
}

(cell, int) load_validators_list(slice custom_s) inline {
    custom_s~load_ref();
    custom_s~load_ref();
    custom_s~load_ref();
    cell config = custom_s~load_dict();
    (cell config_34, int flag?) = config.udict_get_ref?(32, 34);
    throw_unless(err::is_not_keyblock, flag?);
    slice config_34_s = config_34.begin_parse();
    ;; valsType, utimeSince, utimeUntil
    config_34_s~skip_bits(72);
    int total = config_34_s~load_uint(16);
    int main = config_34_s~load_uint(16);
    config_34_s~skip_bits(64); ;; total_weight
    return (config_34_s~load_dict(), main);
}